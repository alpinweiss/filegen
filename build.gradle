plugins {
    id 'java'
    id 'application'
    id 'groovy'
    id 'idea'

    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

ext {
    log4j2Version = '2.4'
    poiVersion = '3.13'
}

sourceCompatibility = 8
targetCompatibility = 8

mainClassName = 'eu.alpinweiss.filegen.AppRunner'

dependencies {
    compile 'com.healthmarketscience.jackcess:jackcess:2.1.2'
    compile 'com.github.mifmif:generex:1.0.1'
    compile 'commons-cli:commons-cli:1.2'
    compile 'com.google.inject:guice:4.0'
    compile 'dom4j:dom4j:1.6.1'

    compile "org.apache.poi:poi:${poiVersion}"
    compile "org.apache.poi:poi-ooxml:${poiVersion}"
    compile "org.apache.poi:poi-ooxml-schemas:${poiVersion}"

    compile "org.apache.logging.log4j:log4j-core:${log4j2Version}"
    compile "org.apache.logging.log4j:log4j-api:${log4j2Version}"

    testCompile 'junit:junit:4.12'
}

shadowJar {
    classifier = 'fat'
}

task depsize {
    doLast {
        final formatStr = "%,10.2f"
        final conf = configurations.default
        final size = conf.collect { it.length() / (1024 * 1024) }.sum()
        final out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} Mb\n\n"
        conf.sort { -it.length() }.each {
            out << "${it.name}".padRight(45)
            out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
        }
        println(out)
    }
}

repositories {
    jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/iovertx-3783/'
    }
}
